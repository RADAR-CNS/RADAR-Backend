configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

//---------------------------------------------------------------------------//
// Source Set                                                                //
//---------------------------------------------------------------------------//
sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + main.compileClasspath
            runtimeClasspath += main.output
        }
        resources {
            srcDir 'src/integrationTest/resources'
        }
    }
}

dependencies {
    // Testing
    testImplementation group: 'junit', name: 'junit', version: junitVersion
    testImplementation group: 'org.mockito', name: 'mockito-core', version: mockitoVersion
    testImplementation group: 'org.hamcrest', name: 'hamcrest-all', version: hamcrestVersion

    // Mock mail server
    testImplementation group: 'org.subethamail', name: 'subethasmtp', version: subethamailVersion

    integrationTestCompile group: 'org.radarcns', name: 'radar-schemas-tools', version: radarSchemasVersion
    testImplementation group: 'log4j', name: 'log4j', version: log4jVersion
    testImplementation group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4jVersion
    integrationTestImplementation group: 'log4j', name: 'log4j', version: log4jVersion
    integrationTestImplementation group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4jVersion
}

tasks.matching {it instanceof Test}.all {
    testLogging {
        showStandardStreams = true
        showExceptions = true
        showCauses = true
        showStackTraces = true
        exceptionFormat "full"
    }
}

task integrationTest(type: Test) {
    useJUnit()
    description = "Run integration tests (located in src/integrationTest/...)."
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    testLogging.events "skipped", "failed", "passed"
}

// Do not do integration test yet, but check if integration tests compile
check.dependsOn integrationTestClasses

test.testLogging.events "skipped", "failed"
