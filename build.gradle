plugins {
    // Apply the java plugin to add support for Java
    id 'java'
    id 'application'
    id 'com.jfrog.bintray' version '1.8.1' apply false

}

//---------------------------------------------------------------------------//
// Configuration                                                             //
//---------------------------------------------------------------------------//

group = 'org.radarcns'
version = '0.4.1-SNAPSHOT'

mainClassName = 'org.radarcns.RadarBackend'
applicationDefaultJvmArgs = ["-Dlog4j.configuration=log4j.properties"]


targetCompatibility = '1.8'
sourceCompatibility = '1.8'

ext {
    moduleDescription = 'Kafka backend for processing device data.'
    githubRepoName = 'RADAR-Base/RADAR-Backend'


    githubUrl = 'https://github.com/' + githubRepoName + '.git'
    issueUrl = 'https://github.com/' + githubRepoName + '/issues'
    website = 'http://radar-base.org'

    codacyVersion = '4.0.2'
    confluentVersion = '5.1.2'
    hamcrestVersion = '1.3'
    kafkaVersion = '2.1.1'
    jacksonVersion = '2.9.8'
    javaMailVersion = '1.6.1'
    junitVersion = '4.12'
    findbugVersion = '3.0.2'
    commonsCliVersion = '1.4'
    mockitoVersion = '2.24.5'
    radarCommonsVersion = '0.12.0-SNAPSHOT'
    radarSchemasVersion = '0.4.4-SNAPSHOT'
    subethamailVersion = '3.1.7'
    jsoupVersion = '1.11.3'
    slf4jVersion = '1.7.25'
    log4jVersion = '1.2.17'
    avroVersion = '1.8.2'
}

//---------------------------------------------------------------------------//
// Sources and classpath configurations                                        //
//---------------------------------------------------------------------------//

// In this section you declare where to find the dependencies of your project
repositories {
    mavenCentral()
    // Non-jcenter radar releases
    maven { url 'http://dl.bintray.com/radar-cns/org.radarcns' }
    // Kafka/confluent releases
    maven { url 'http://packages.confluent.io/maven/' }
    // For working with dev-branches
    maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
    // Github code
    maven { url 'https://jitpack.io' }
}

configurations {
    implementation {
        exclude group: 'org.slf4j', module: 'slf4j-log4j12'
        exclude group: 'log4j', module: 'log4j'
        exclude group: 'jline', module: 'jline'
        exclude group: 'io.netty', module: 'netty'
    }
}

// In this section you declare the dependencies for your production and test code
dependencies {
    implementation group: 'org.radarcns', name: 'radar-commons', version: radarCommonsVersion
    implementation group: 'org.apache.avro', name: 'avro', version: avroVersion
    implementation group: 'org.radarcns', name: 'radar-commons-testing', version: radarCommonsVersion
    implementation group: 'org.radarcns', name: 'radar-schemas-commons', version: radarSchemasVersion

    // Kafka streaming API
    implementation group: 'org.apache.kafka', name: 'kafka-streams', version: kafkaVersion
    implementation group: 'io.confluent', name: 'kafka-streams-avro-serde', version: confluentVersion

    // Nonnull annotation
    implementation group: 'com.google.code.findbugs' , name: 'jsr305' , version: findbugVersion

    // Command-line interface
    implementation group: 'commons-cli', name: 'commons-cli', version: commonsCliVersion

    // Configuration @JsonProperty
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: jacksonVersion

    // Monitor mail sending
    implementation group: 'javax.mail', name: 'javax.mail-api', version: javaMailVersion
    implementation group: 'com.sun.mail', name: 'javax.mail', version: javaMailVersion

    // JSoup html parser
    implementation group: 'org.jsoup', name: 'jsoup', version: jsoupVersion

    runtimeOnly group: 'org.radarcns', name: 'radar-commons-unsafe', version: radarCommonsVersion

    runtimeOnly group: 'log4j', name: 'log4j', version: log4jVersion
    runtimeOnly group: 'org.slf4j', name: 'slf4j-log4j12', version: slf4jVersion
}

if (!hasProperty('profile')) {
    ext.profile = 'dev'
}

apply from: "gradle/profile.${profile}.gradle"
